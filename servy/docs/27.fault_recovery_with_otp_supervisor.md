# Fault Recovery with OTP Supervisor

## 강의 주요 내용

* Supervisor 개념
* Supervisor children 등록
* Supervisor -> children argument 전달
* Supervisor of service supervisor
### 1. Supervisor 개념

* GenServer를 위한 Supervisor를 배운다.

![[프로세스링크 1.png]]

### 2. Supervisor children 등록

* OTP Supervisor는 children을 등록하고, children의 `start_link` 함수를 호출한다.
```elixir
defmodule Servy.ServicesSupervisor do
  ## Supervisor 프로세스임을 명시
  use Supervisor  

  def start_link do
    IO.puts "Starting the services supervisor..."
    ## start_link 함수를 호출하면, init 함수가 호출됨
    Supervisor.start_link(__MODULE__, :ok, name: __MODULE__)
  end  

  def init(:ok) do
    children = [
      Servy.PledgeServer,
      Servy.SensorServer
    ]

	## children의 start_link 함수를 호출함
	## one_for_one 옵션은 하나의 children이 종료되면, 그 프로세스만 재기동
	## one_for_all 옵션을 넣으면, 하나가 종료되더라도 전체를 재기동
    Supervisor.init(children, strategy: :one_for_one)
  end
end
```

```elixir
iex(1)> {:ok, sup_pid} = Servy.ServicesSupervisor.start_link()
Starting the services supervisor...
Starting the pledge server...
Starting the sensor server...
Running tasks to get sensor data...
{:ok, #PID<0.204.0>}

iex(2)> Supervisor.which_children(sup_pid)
[
  {Servy.SensorServer, #PID<0.206.0>, :worker, [Servy.SensorServer]},
  {Servy.PledgeServer, #PID<0.205.0>, :worker, [Servy.PledgeServer]}
]

iex(3)> Supervisor.count_children(sup_pid)
%{active: 2, workers: 2, supervisors: 0, specs: 2}

## 종료와 동시에 다시 서버가 시작되는 로그를 볼수 있다.
iex(4)> Process.whereis(:sensor_server) |> Process.exit(:kaboom)
true
Starting the sensor server...
Running tasks to get sensor data...
```


### 3. Supervisor -> children argument 전달

```elixir
  ## Supervisor 모듈
  ...
  ...
  def init(:ok) do
    children = [
      Servy.PledgeServer,
      ## 첫번째는 모듈명이고, 두번째가 argument 이다. 자료형은 해당 모듈에 맞는 어떤것이라도 가능하다
      {Servy.SensorServer, 60}
    ] 

    Supervisor.init(children, strategy: :one_for_one)
  end


  ## SendorServer
  ..
  ..

  ## Supervisor의 argument가 start_link 함수로 들어온다.
  def start_link(interval) do
    IO.puts "Starting the sensor server with #{interval}..."
    GenServer.start_link(__MODULE__, %{}, name: @name)
  end
```


### 5. Supervisor of service supervisor

* KickStarter나 ServicesSupervisor가 죽으면 어떻게 될까?
* 이 서버들 또한 누군가에 의해 모니터링되고 재기동 되어야 한다. 간단하게 `Supervisor`라 하자

![[Pasted image 20240422150523.png]]

```elixir
defmodule Servy.Supervisor do
  use Supervisor  

  def start_link do
    IO.puts "Starting THE supervisor..."
    Supervisor.start_link(__MODULE__, :ok, name: __MODULE__)
  end  

  def init(:ok) do
    ## Supervisor 역할을 하는 프로세스를 children으로 등록한다.
    children = [
      Servy.KickStarter,
      Servy.ServicesSupervisor,
    ] 

    Supervisor.init(children, strategy: :one_for_one)
  end
end
```

```elixir
iex(6)> pid = Process.whereis(Servy.ServicesSupervisor)
#PID<0.216.0>

## ServiceSupervisor 프로세스 종료시 즉시 재기동되고, 이하 process도 재기동된다.
iex(7)> Process.exit(pid, :kill)
Starting the services supervisor...
true
Starting the pledge server...
Starting the sensor server with 60...
Running tasks to get sensor data...
```
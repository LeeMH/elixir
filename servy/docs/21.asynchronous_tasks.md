# Asynchronous Tasks

## 강의 주요 내용

* 메세지 순서 보장 (receive 패턴매칭)
* Fetcher 모듈의 일반화
* Task 모듈로 변환
* Task 모듈 추가 설명


### 1. 메세지 순서 보장 (receive 패턴매칭)

* 메세지 박스는 FIFO 구조이다.
* 1, 2, 3 job 순서로 실행했더라고, receive에서 1, 2, 3 순서로 결과가 리턴 되지는 않는다.
* 따라서, 명시적으로 자신의 메세지를 읽기 위해서는 응답에 `처리 프로세스 pid`를 추가해 주어야 한다.

```elixir
iex(1)> parent = self()
#PID<0.187.0>

## 비동기 작업 수행, 비동기 작업 수행시 해당 프로세스의 pid가 리턴된다.
## 메세지의 첫번째 요소로, 처리 프로세스의 pid를 추가 했다.
iex(2)> pid1 = spawn(fn -> send(parent, {self(), :result, "snapshot3.jpg"}) end)
#PID<0.188.0>

iex(3)> pid2 = spawn(fn -> send(parent, {self(), :result, %{x: 1, y: 1}}) end)
#PID<0.189.0>

iex(4)> pid3 = spawn(fn -> send(parent, {self(), :result, "snapshot1.jpg"}) end)
#PID<0.190.0>

## 3개의 작업 수행후 메세지함
iex(5)> Process.info(parent, :messages)
{:messages,
 [
   {#PID<0.188.0>, :result, "snapshot3.jpg"},
   {#PID<0.189.0>, :result, %{y: 1, x: 1}},
   {#PID<0.190.0>, :result, "snapshot1.jpg"}
 ]}

## pid2번의 작업 결과를 얻기 위해, 패턴매칭에 pid2를 넣는다.
## ^는 핀 연산으로 값을 할당하지 않겠다는 뜻이다. 따라서, pid2와 값이 같은경우만 매칭된다.
iex(6)> receive do {^pid2, :result, coords} -> coords end
%{y: 1, x: 1}
```


### 2. Fetcher 모듈의 일반화

* job을 비동기로 실행하고, 해당 결과를 수신하는 것은 아주 일반적인 flow 이다.
* handler에서 VideoCam.get_snapshot 함수를 직접 호출하던 부분을 아래처럼 함수화 할 수 있다.

```elixir
defmodule Servy.Fetcher do
  ## 비동기 작업을 Fetcher.async로 함수로 구성
  def async(camera_name) do
    parent = self()
    spawn(fn -> send(parent, {:result, Servy.VideoCam.get_snapshot(camera_name)}) end)
  end

  ## receive 작업을 함수로 구성
  def get_result() do
    receive do {:result, filename} -> filename end
  end
end

## hander 사이드
Fetcher.async("cam-1")
Fetcher.async("cam-2")
Fetcher.async("cam-3")

snapshot1 = Fetcher.get_result()
snapshot2 = Fetcher.get_result()
snapshot3 = Fetcher.get_result()
```

* 이를 좀 더 일반화 시켜보면, `어떤 함수를 호출`하고, `결과를 수신` 이렇게 표현할 수 있다.
* 아래는 좀 더 일반화된 코드로 변환한 예제이다.
```elixir
defmodule Servy.Fetcher do
  ## 함수를 입력받아 해당 함수를 비동기로 실행한다.
  def async(fun) do
    spawn(fn -> send(parent, {:result, fun.()}) end)
  end

  def get_result() do
    receive do {:result, value} -> value end
  end
end
```

* 문제가 해결된 것 같지만, receive에서 명확하게 자신의 메세지를 읽을 수 없다.
* FIFO 방식이기 때문에, 먼저 종료된 프로세스의 결과가 receive 순서에 따라 리턴된다.
* 이를 해결하기 위해, pid를 이용하여 명시적으로 자신의 메세지를 receive하게 개선한다.

```elixir
defmodule Servy.Fetcher do
  def async(fun) do
    parent = self()
    ## 어떤 프로세스의 결과인지 명시적으로 나타내기 위해, 리턴 메세지의 첫번째에 pid를 추가한다.
    spawn(fn -> send(parent, {self(), :result, fun.()}) end)
  end

  ## receive하고자 하는 pid를 넣어서 명시적으로 해당 프로세스의 결과를 수신한다.
  def get_result(pid) do
    ## 핀연산자(^)를 이용해서, 해당 pid와 일치하는 값만 수신한다.
    ## 만약, 해당 pid의 값이 없다면 receive는 메세지가 수신될 때까지 블로킹 된다.
    receive do {^pid, :result, value} -> value end
  end
end


## hander 사이드
  def route(%Conv{ method: "GET", path: "/sensors" } = conv) do
    snapshots =
      ["cam-1", "cam-2", "cam-3"]
      ## map 함수를 통해, 프로세스의 List<pid> 형태로 변환된다. spawn의 결과는 프로세스의 pid이다.
      |> Enum.map(&Fetcher.async(fn -> VideoCam.get_snapshot(&1) end))
      ## get_result의 파라메터로 pid를 넣고, 해당 메세지의 결과를 수신한다.
      |> Enum.map(&Fetcher.get_result(&1))  

    ## get_location 함수를 호출하고, 그 결과를 receive 한다.
    task = Fetcher.async(fn -> Servy.Tracker.get_location("bigfoot") end)
    where_is_bigfoot = Fetcher.get_result(task)  

    %{ conv | status: 200, resp_body: inspect {snapshots, where_is_bigfoot}}
  end
```

### 3. Task 모듈로 변환

* 앞서 정리한 대로, Fetcher 모듈을 통해 `비동기 실행/결과 수신` flow을 일반화 하였다.
* `비동기 실행/결과 수신`은 너무 일반적인 flow 이기 때문에 이미 `Task` 모듈에 구현되어 있다.
* 현재 까지 구현된 내용을 Task 형태로 변환 한다.

```elixir
  def route(%Conv{ method: "GET", path: "/sensors" } = conv) do
    snapshots =
      ["cam-1", "cam-2", "cam-3"]
      ## Fetcher.async -> Task.async 로 변경. 결과값은 pid와 비슷한 task 이다.
      |> Enum.map(&Task.async(fn -> VideoCam.get_snapshot(&1) end))
      ## Fetcher.get_result -> await 로 변경. 수행하는 동작은 동일하다.
      |> Enum.map(&Task.await(&1))  

    task = Task.async(fn -> Servy.Tracker.get_location("bigfoot") end)
    where_is_bigfoot = Task.await(task)  

    %{ conv | status: 200, resp_body: inspect {snapshots, where_is_bigfoot}}
  end
```


### 4. Task 모듈 추가 설명

#### 4.1 Task 사용 방법

```elixir
task = Task.async(fn -> Servy.Tracker.get_location("bigfoot") end)

## MFA 방식 => Module, Function, Arguments
## 이때, 함수는 atom으로 처리되기 때문에 콜론(:)을 붙여서 호출한다.
task = Task.async(Servy.Tracker, :get_location, ["bigfoot"])
```

#### 4.2 receive 타임아웃

```elixir
def get_result(pid) do
  receive do
    {^pid, :result, value} -> value
  ## 2초 타임아웃을 설정한다.
  after 2000 ->    raise "Timed out!"  end
end

## iex 실행결과
iex> pid = Servy.Fetcher.async(fn -> :timer.sleep(4000) end)

iex> Servy.Fetcher.get_result(pid)
** (RuntimeError) Timed out!
```

#### 4.3 Task.await 타임아웃

* `Task.await 디폴트 타임아웃은 5초` 이다.
```elixir
iex> task = Task.async(fn -> :timer.sleep(7000); "Done!" end)

## 디폴트 타임아웃은 5초이기 때문에, 타임아웃이 발생한다.
iex> Task.await(task)
** (exit) exited in: Task.await(%Task{owner: #PID<0.163.0>, pid: #PID<0.200.0>, ref: #Reference<0.1371140241.2411724801.162602>}, 5000)
    ** (EXIT) time out

## 타임아웃을 8초로 설정한다.
iex> Task.await(task, 8000)
"Done!"

## 타임아웃을 무한대로 설정한다.
iex> Task.await(task, :infinity)
"Done!"
```

#### 4.4 Task.yield 

* await가 결과가 수신 될 때까지 기다리는 반면, `yield`는 결과가 없으면 `nil을 즉시 반환` 한다.
```elixir
iex> task = Task.async(fn -> :timer.sleep(8000); "Done!" end)

iex> Task.yield(task, 5000)
nil

iex> Task.yield(task, 5000)
{:ok, "Done!"}
```

#### 4.5 cut-off 타임

* `yield`를 활용하여, cut-off 작업을 구현할 수 있다.
* 만약 shutdown 도중 메세지가 도착하면, `{:ok, result}를 반환`하고, 아니면 `nil을 반환`한다.
```elixir
## 5초간 메세지를 수신한다.
case Task.yield(task, 5000)
  {:ok, result} ->
    result
  ## 결과가 없다면(nil), task를 shutdown하고 nil을 리턴한다.
  nil ->
    Logger.warn "Timed out!"
    Task.shutdown(task)
end
```

#### 4.6 human-readable 설정

```elixir
iex> :timer.seconds(5)
5000

iex> :timer.minutes(5)
300000

iex> :timer.hours(5)
18000000

## await의 입력은 밀리세컨드이지만, timer 모듈을 이용하여, 일반적인 시간으로 처리할 수 있다.
iex> Task.await(task, :timer.seconds(7))
```
# Stateful Server Processes

## 강의 주요 내용

* stateful 서버 구성
* stateful 서버 예제
* stateful 서버 캡슐화 및 코드 개선
* 메세지 박스 full 방지를 위한 코드
* server-side run, client-side run
* 동시성(concurrent, parallel), 순차처리(serialized)
* Agents 모듈

### 1. stateful 서버 구성

* elixir에서는 OOP 처럼 별도의 상태를 공유할 방법이 없다.
* listen_loop에 초기값을 넣고, 해당 값을 메모리 상에서 유지하는 방법으로 처리한다.
![[state관리.png]]

### 2. stateful 서버 예제

```elixir
defmodule Servy.PledgeServer do  
  
  ## 상태유지를 위해 state를 받는다.
  def listen_loop(state) do
    IO.puts "\nWaiting for a message..." 

    receive do
      ## 생성인경우
      {:create_pledge, name, amount} ->
        {:ok, id} = send_pledge_to_servcie(name, amount)
        ## 기존 state 앞에 새로 생성된 pledge를 추가한다.
        new_state = [ {name, amount} | state ]
        IO.puts "#{name} pledged #{amount}!"
        IO.puts "New state is #{inspect new_state}"
        ## 해당 상태를 유지하기 위해, 다시 listen_loop에 해당값을 전달한다.
        listen_loop(new_state)
      ## cache 조회인경우,
      {sender, :recent_pledges} ->
        ## 요청자에게 결과를 돌려주어야 하기때문에, sender(pid)를 받아 send로 결과를 회신한다.
        send sender, {:response, state}
        IO.puts "Sent pledges to #{inspect sender}"
        
        ## 변경이 없지만, 상태를 유지하기 위해 다시 argument로 상태를 전달한다.
        listen_loop(state)
    end
  end

  ...
  ...
end  
```

```elixir
## PledgeServer 시작
## 마지막은 arguement를 list로 넣어야 한다. 첫번째 argument로 empty list를 전달한다.
iex(6)> pid = spawn(Servy.PledgeServer, :listen_loop, [[]])

Waiting for a message...
#PID<0.213.0>

## larry 생성
iex(7)> send pid, {:create_pledge, "larry", 10}
larry pledged 10!
{:create_pledge, "larry", 10}
New state is [{"larry", 10}]

Waiting for a message...

## moe 생성
iex(8)> send pid, {:create_pledge, "moe", 20}
moe pledged 20!
{:create_pledge, "moe", 20}
New state is [{"moe", 20}, {"larry", 10}]

Waiting for a message...

## curly 생성
iex(9)> send pid, {:create_pledge, "curly", 30}
curly pledged 30!
{:create_pledge, "curly", 30}
New state is [{"curly", 30}, {"moe", 20}, {"larry", 10}]

Waiting for a message...

## recent_pledge(cache) 요청
iex(10)> send pid, {self(), :recent_pledges}
Sent pledges to #PID<0.202.0>
{#PID<0.202.0>, :recent_pledges}

Waiting for a message...

## 프로세스간 통신은 메시지 박스를 통해서만 이뤄진다.
## 메세지 박스를 확인하면, 수신된 메시지를 확인할 수 있다.
iex(11)> Process.info(self(), :messages)
{:messages, [response: [{"curly", 30}, {"moe", 20}, {"larry", 10}]]}

## 메세지를 받는다.
iex(12)> receive do {:response, v} -> v end
[{"curly", 30}, {"moe", 20}, {"larry", 10}]
iex(13)> 
```


### 3. stateful 서버 캡슐화 및 코드 개선


```elixir
defmodule Servy.PledgeServer do
  ## @name으로 pid alias(?)를 상수화 한다.
  @name :pledge_server  

  ## 서버 모듈(상태)의 시작과 상세한 내용을 start 함수로 캡슐화 한다.
  def start do
    IO.puts "Starting the pledge server..."
    
    ## __MODULE__을 사용하면, 컴파일 타임에 자신이 속해있는 모듈로 변경되어 캄파일 된다.    
    pid = spawn(__MODULE__, :listen_loop, [[]])
    
    ## PID를 :pledge_server로 등록한다.
    Process.register(pid, @name)
    
    pid
  end  

  def listen_loop(state) do
    IO.puts "\nWaiting for a message..."  

    receive do
      {sender, :create_pledge, name, amount} ->
        {:ok, id} = send_pledge_to_servcie(name, amount)
        most_recent_pledges = Enum.take(state, 2)
        new_state = [ {name, amount} | most_recent_pledges ]  

        ## 요청자에게 응답 전송
        send sender, {:response, id}  
        listen_loop(new_state)

      {sender, :recent_pledges} ->
        send sender, {:response, state}
        listen_loop(state)
    end
  end

  ## pid가 @name으로 alias화 되었으므로 함수호출시 pid를 받을 필요 없다.
  def create_pledge(name, amount) do
    ## @name은 서버의 pid와 동일
    send @name, {self(), :create_pledge, name, amount}

    ## create에 대한 응답을 리턴하기 위해, 동일하게 receive 작업을 수행한다.
    receive do {:response, status} -> status end
  end  

  def recent_pledges() do
    ## @name은 서버의 pid와 동일
    send @name, {self(), :recent_pledges}
    receive do {:response, pledges} -> pledges end
  end
```

### 4. 메세지 박스 full 방지를 위한 코드

* receive 에서 패턴에 매칭되지 않으면 메세지 박스에 계속 저장된다.
* full 방지를 위해, default clause를 추가 한다.
```elixir
  def listen_loop(state) do

    IO.puts "\nWaiting for a message..."  

    receive do
      {sender, :create_pledge, name, amount} ->
        {:ok, id} = send_pledge_to_servcie(name, amount)
        most_recent_pledges = Enum.take(state, 2)
        new_state = [ {name, amount} | most_recent_pledges ] 
        send sender, {:response, id}  
        listen_loop(new_state)  

      {sender, :recent_pledges} ->
        send sender, {:response, state}
        listen_loop(state)
        {sender, :total_pledged} ->
          total =
            Enum.map(state, &elem(&1, 1))
            |> Enum.sum
          send sender, {:response, total}
          listen_loop(state)

      ## 매칭되지 않는 메세지 처리를 위한 default clause
      ## 메세지 박스 full 방지를 위해 꼭 필요하다.
      unexpected ->
        IO.puts "Unexpected messaged: #{inspect unexpected}"
        listen_loop(state)
    end
  end
```


### 5. server-side run, client-side run

* 예제의 pledge_server는 같은 모듈의 함수라도 `서버 사이드`, `클라이언트 사이드` 각각 실행되는 위치가 다르다.
* 결국 `모듈`은 `함수를 모아 놓은 bucket` 같은 개념이다.

```elixir
  ## server Side run
  ## spawn 되어 listen_loop는 서버의 별도 프로세스에서 실행된다.
  def start do
    IO.puts "Starting the pledge server..."
    pid = spawn(__MODULE__, :listen_loop, [[]])
    ## PID를 :pledge_server로 등록한다.
    Process.register(pid, @name)
    pid
  end

  ## client Side run
  ## 반면, 인터페이스 함수들은 controller를 통해서, 클라이언트 프로세스에서 실행된다.
  def create_pledge(name, amount) do
    send @name, {self(), :create_pledge, name, amount}
    receive do {:response, status} -> status end
  end  

  def recent_pledges() do
    send @name, {self(), :recent_pledges}
    receive do {:response, pledges} -> pledges end
  end

```


### 6. 동시성(concurrent, parallel), 순차처리(serialized)

* 동시에 N개의 요청이 발생하면,
* `client-side`에서는 `동시성`으로 처리된다.
* 반면, `server-size` 에서는 `순차처리` 된다.
* 서버의 receive에서 메세지를 fetch하고, 처리후, listen_loop를 다시 호출하기 때문이다.
* 물론, 동시 처리를 원하면 spawn 시키면 되지만, 이럴 경우 `동시성 문제로 cache가 깨질 확율`이 높다.
![[Pasted image 20240418193522.png]]


### 7. Agent 모듈

* elixir에서는 간단한 상태 모듈 Agent를 제공한다.
```elixir

## 상태 모듈 생성
iex(4)> {:ok, agent} = Agent.start(fn -> [] end)
{:ok, #PID<0.233.0>}

## 상태 추가
iex(5)> Agent.update(agent, fn(state) -> [ {"larry", 10} | state ] end)
:ok

iex(6)> Agent.update(agent, fn(state) -> [ {"moe", 20} | state ] end)
:ok

## 상태 조회
iex(7)> Agent.get(agent, fn(state) -> state end)
[{"moe", 20}, {"larry", 10}]
```
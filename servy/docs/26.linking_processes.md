# Another GenServer

## 강의 주요 내용

* OTP 서버의 모니터링, restart 개념
* HTTPServer 모니터링, restart 전용 KickStarter 서버 추가
* KickStarter, HTTPServer link
* KickStarter -> HTTPServer 모니터링 하도록 개선

### 1. OTP 서버의 모니터링, restart 개념

* elixir는 전화 교환망으로 개발된 erlang 위에서 실행되는 언어이다.
* GenServer는 OTP Supervisor로 모니터링, 재기동이 가능하다.
* 우선 일반 프로세스인 HTTPServer 전용 KickStarter를 만들며, Supervisor 기능이 어떻게 돌아가는지 이해해 본다.

![[프로세스링크.png]]

### 2. HTTPServer 모니터링, restart 전용 KickStarter 서버 추가

* init 함수를 통해 HTTPServer를 start 시키고, 프로세스로 등록한다.
```elixir
defmodule Servy.KickStarter do
  use GenServer  

  def start do
    IO.puts "Starting the kickstarter"
    GenServer.start(__MODULE__, :ok, name: __MODULE__)
  end  

  def init(:ok) do
    IO.puts "Starting the HTTP server..."
    ## HTTPServer를 기동하고, 프로세스를 등록한다.
    server_pid = spawn(Servy.HttpServer, :start, [4000])
    Process.register(server_pid, :http_server)
    {:ok, server_pid}
  end
end
```


```elixir
## KickStarter 서버를 기동한다.
## init에서 HTTPServer를 동시에 기동하는 로그가 출력된다.
iex(1)> {:ok, kick_pid} = Servy.KickStarter.start()
Starting the kickstarter
Starting the HTTP server...

🎧  Listening for connection requests on port 4000...

⌛️  Waiting to accept a client connection...

{:ok, #PID<0.201.0>}

## HTTPServer PID를 확인한다.
iex(2)> server_pid = Process.whereis(:http_server)
#PID<0.202.0>

## HTTPServer를 종료한다.
iex(3)> Process.exit(server_pid, :kaboom)
true

## HTTPServer가 종료된것을 확인할 수 있다.
iex(4)> Process.alive?(server_pid)
false

## KickStarter 서버는 종료되지 않았다.
iex(5)> Process.alive?(kick_pid)
true
```

### 3. KickStarter, HTTPServer link

* KickStarter와 HTTPServer를 연결한다.
* link로 연결하는 경우, `연결은 양방향`이다.
```elixir
defmodule Servy.KickStarter do
  ...
  ...
  def init(:ok) do
    IO.puts "Starting the HTTP server..."
    server_pid = spawn(Servy.HttpServer, :start, [4000])

    ## KickStarter와 HTTPServer를 link 한다.
    Process.link(server_pid)
    Process.register(server_pid, :http_server)
    {:ok, server_pid}
  end
end
```

```elixir
iex(1)> {:ok, kick_pid} = Servy.KickStarter.start()
Starting the kickstarter
Starting the HTTP server...

🎧  Listening for connection requests on port 4000...

⌛️  Waiting to accept a client connection...

{:ok, #PID<0.200.0>}

iex(2)> server_pid = Process.whereis(:http_server)
#PID<0.201.0>

## 연결된 프로세를 확인가능하다.
iex(3)> Process.info(kick_pid, :links)
{:links, [#PID<0.201.0>]}

## HTTPServer 역시 KickStarter가 연결되어 있다.
iex(4)> Process.info(server_pid, :links)
{:links, [#PID<0.200.0>, #Port<0.4>]}

## HTTPServer 종료
iex(5)> Process.exit(server_pid, :kaboom)
true

## 앞의 예제와 다르게, KickStarter도 종료되어 있다.
iex(6)> Process.alive?(kick_pid)
false
```

### 4. KickStarter -> HTTPServer 모니터링 하도록 개선

* exit 시그널 처리
```elixir
  def init(:ok) do
    ## 해당 시그널을 받더라도, 같이 종료되지 않게 설정한다.
    Process.flag(:trap_exit, true)
    IO.puts "Starting the HTTP server..."
    server_pid = spawn(Servy.HttpServer, :start, [4000])
    Process.link(server_pid)
    Process.register(server_pid, :http_server)
    {:ok, server_pid}
  end
```

```elixir
iex(1)> {:ok, kick_pid} = Servy.KickStarter.start()
Starting the kickstarter
Starting the HTTP server...

🎧  Listening for connection requests on port 4000...

⌛️  Waiting to accept a client connection...

{:ok, #PID<0.200.0>}

iex(2)> server_pid = Process.whereis(:http_server)
#PID<0.201.0>

## HTTPServer 종료
iex(3)> Process.exit(server_pid, :kaboom)
true

## KickStarter에서 아래 메세지를 수신했다는 뜻이다.
## handle_info 함수를 통해 아래 메세지를 핸들링하고, HTTPServer를 재기동하면 된다.
11:59:14.506 [error] Servy.KickStarter Servy.KickStarter received unexpected message in handle_info/2: {:EXIT, #PID<0.201.0>, :kaboom}

## KickStarter는 종료되지 않았다!!!
iex(4)> Process.alive?(kick_pid)
true
```
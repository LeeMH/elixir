# Comprehensions

## 강의 주요 내용

* eex 템플릿 파일 처리
* comprehensions 방식으로 개선
* default parameter value
* comrehensions 예제

### 1. eex 템플릿 파일 처리

* eex(Embedded Elixir)를 활용하여, 템플릿 파일을 읽고 렌더링 한다.

```elixir
<h1>All The Bears!</h1>

<ul>
  ## bears를 Enum.map을 이용해서 하나씩 fetch 하여 렌더링 한다.
  <%= Enum.map(bears, fn(bear) -> %>
    <li> <%= bear.name %> - <%= bear.type %></li>
  <% end %>
</ul>
```

* Enum.map 부분에도 템플릿이 `<%=`으로 시작하는 것에 주의하자!
* 무언가 아웃풋이 있으면 모두 `<%=`로 시작한다고 생각하면 간단하다.
* 바인딩 된 값을 검사하고 싶은경우, 아래 처럼 사용하면 된다.
```elixir
<%= inspect(bears) %>
```

* 템플릿 파일 호출 및 변수 바인딩
```elixir

  ## 템플릿 파일 경로를 상수로 선언한다.
  @templates_path Path.expand("templates", File.cwd!)
  
  def index(conv) do
    bears =
      Wildthings.list_bears()
      ## sort ascending
      |> Enum.sort(&Bear.order_asc_by_name/2)

	## 템플릿 파일 경로에 파일(index.eex)를 연결하고
    content = @templates_path
    |> Path.join("index.eex")
    ## EEx 모듈을 이용하여 렌더링 한다.
    |> EEx.eval_file(bears: bears)

    %{ conv | status: 200, resp_body: content }
  end
```


### 2. comprehensions 방식으로 개선

* 정상적으로 처리되지만, eex 파일의 Enum.map 부분의 가독성이 좋지는 않다.
* 간단하게 표현할 수 있는 `comprehension` 방식을 사용하여 개선한다.

```elixir
<h1>All The Bears!</h1>  

<ul>
  <%= for bear <- bears do %>
    <li> <%= bear.name %> - <%= bear.type %></li>
  <% end %>
</ul>
```

* 일반 언어의 for each 형태로 구성되어 가독성이 좋다.

### 3. default parameter value

```elixir
  ## bindings에 값이 들어오지 않으면, empty list를 default value로 사용한다.
  defp render(conv, template, bindings \\ []) do
    content = @templates_path
    |> Path.join(template)
    |> EEx.eval_file(bindings)  

    %{ conv | status: 200, resp_body: content }
  end
```


### 4. comprehensions 예제

```elixir
iex(17)> nums = [1, 2, 3]
[1, 2, 3]

## 일반적으로 사용하는 Enum.map 함수
iex(18)> Enum.map(nums, fn(n) -> n * 3 end)
[3, 6, 9]

## comprehension 방식을 사용한 방법
iex(19)> for x <- nums, do: x * 3
[3, 6, 9]

## 아래와 같이, 복수개의 list에 대해서도 comprehension 방법을 적용할 수 있다.
iex(20)> for size <- ["S", "M", "L"], color <- [:red, :blue], do: {size, color}
[
  {"S", :red},
  {"S", :blue},
  {"M", :red},
  {"M", :blue},
  {"L", :red},
  {"L", :blue}
]
```


```
## 예제 데이터 생성
iex(23)> prefs = [ {"Betty", :dog}, {"Bob", :dog}, {"Becky", :cat} ]
[{"Betty", :dog}, {"Bob", :dog}, {"Becky", :cat}]

## 패턴 매칭을 이용하여 dog 데이터만 추출.
iex(24)> for {name, :dog} <- prefs, do: name
["Betty", "Bob"]

iex(25)> for {name, :cat} <- prefs, do: name
["Becky"]

## filter expression을 사용하여 dog 데이터만 추출
## 리스트 뒤에 컴마(,)를 찍고 filter 조건을 나열한다.
iex(26)> for {name, pet_choice} <- prefs, pet_choice == :dog, do: name
["Betty", "Bob"]

## 필터로 사용할 조건을 익명함수로 저장한다.
iex(27)> dog_lover? = &(&1 == :dog)
#Function<42.105768164/1 in :erl_eval.expr/6>

iex(28)> cat_lover? = &(&1 == :cat)
#Function<42.105768164/1 in :erl_eval.expr/6>

## filter expression에 위에서 작성한 함수를 적용한다.
## 익명함수를 호출할때 점(.)을 추가하는 것을 잊지 말자.
iex(29)> for {name, pet_choice} <- prefs, dog_lover?.(pet_choice), do: name
["Betty", "Bob"]

iex(30)> for {name, pet_choice} <- prefs, cat_lover?.(pet_choice), do: name
["Becky"]
```
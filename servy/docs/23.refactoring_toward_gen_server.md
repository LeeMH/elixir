# Refactoring Toward GenServer

## 강의 주요 내용

* 코드 중복 및 기능 중복
* 일반화된 서버로 리팩토링

### 1. 코드 중복 및 기능 중복

* 아래 그림과 같이, 코드와 기능이 중복된 부분이 있다.
* 클라이언트 인터페이스는 `요청하고`, `결과수신` 이렇게 추상화 할 수 있다.
* 서버 프로세스는 `무엇인가 처리`하고, `회신`하고, `listen_loop` 실행 이렇게 추상화 할 수 있다.
![[코드중복.png]]

### 2. 일반화된 서버로 리팩토링

```
defmodule Servy.GenericServer do
  
  ## 일반화된 서버를 위해, 초기 상태, callback, name을 argument로 받아서 start 한다.
  ## callback_module은 요청자의 module이다. 상태관리, 분기만 GenericServer에서 하고
  ## 실제 로직은 각각의 업무의 함수를 호출하기 위해, callback_module을 관리한다.
  def start(callback_module, initial_state, name) do
    IO.puts "Starting the pledge server..."
    pid = spawn(__MODULE__, :listen_loop, [initial_state, callback_module])
    
    ## @name으로 등록하던것을, argument로 받아서 등록한다.
    Process.register(pid, name)
    pid
  end

  ## 요청을 주고, 받는 것은 아주 일반화된 로직이다.
  ## 따라서, 변하는 message만 argument로 받아서 추상화 한다.
  def call(pid, message) do
    ## listen_loop에서 call과 cast 요청을 구분하기 위해, 첫번째 값으로 call atom을 전달한다.
    send pid, {:call, self(), message}
    receive do {:response, response} -> response end
  end  

  ## cast 역시, message만 수신하여 일반화된 로직으로 처리한다.
  def cast(pid, message) do
    send pid, {:cast, message}
  end  

  ## callback_module은 요청자의 모듈이다.
  ## 각각의 로직은 해당 모듈에 존재하기 때문에 callback_module을 통해 로직을 호출한다.
  ## 여기서는 마치 인터페이스처럼 handle_call/2, handle_cast/2 함수가 요청자에 구현되어 있어야 한다.
  def listen_loop(state, callback_module) do
    receive do
      ## :call 요청을 처리
      {:call, sender, message} when is_pid(sender) ->         
        {response, new_state} = callback_module.handle_call(message, state)
        send sender, {:response, response}
        listen_loop(new_state, callback_module)

      ## :cast 요청을 처리
      {:cast, message} ->
        new_state = callback_module.handle_cast(message, state)
        listen_loop(new_state, callback_module)
        
      ## 메세지 박스에 매칭되지 않는 메세지가 계속 쌓이는것을 방지하기 위해 default 절을
      unexpected ->
        IO.puts "Unexpected messaged: #{inspect unexpected}"
        listen_loop(state, callback_module)
    end
  end

end
```

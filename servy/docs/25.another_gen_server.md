# Another GenServer

## 강의 주요 내용

* Schedule Job 추가

### 1. Schedule Job 추가

* schedule job을 추가하는 방법에 대해서 설명한다.
* spring 에서 애노테이션등으로 설정하는 방법이 아니고, 소스상에 작은(?) 코딩을 추가하는 방법이다.
* 서버가 생성될 때(`init 함수`), `Process.send_after 함수`를 이용해서 cache 갱신 메세지를 발행한다.
* send, cast가 아니기 때문에, `handle_info`에서 메세지를 수신하여 처리한다.
* cache 갱신 후, `다시 갱신 메세지를 발행`한다.

```elixir

## 캐쉬갱신을 담당하는 서버를 생성한다.
defmodule Servy.SensorServer do
  @name :sensor_server
  @referesh_interval :timer.seconds(5)  

  use GenServer  

  # Client Interface
  def start do
    GenServer.start(__MODULE__, %{}, name: @name)
  end  

  def get_sensor_data do
    GenServer.call @name, :get_sensor_data
  end  

  # Server Callbacks
  def init(_state) do
    initial_state = run_tasks_to_get_sensor_data()
    ## 5초후 refresh 요청 발행
    schedule_refresh()
    {:ok, initial_state}
  end  

  def handle_info(:refresh, _state) do
    IO.puts "Refreshing the cache..."
    new_state = run_tasks_to_get_sensor_data()
    ## 캐쉬 갱신후 다시 갱신 요청 만들기
    schedule_refresh()
    {:noreply, new_state}
  end  

  defp schedule_refresh do
    Process.send_after(self(), :refresh, @referesh_interval)
  end  

  def handle_call(:get_sensor_data, _from, state) do
    {:reply, state, state}
  end  

  defp run_tasks_to_get_sensor_data do
    IO.puts "Running tasks to get sensor data..."
    task = Task.async(fn -> Servy.Tracker.get_location("bigfoot") end) 

    snapshots =
      ["cam-1", "cam-2", "cam-3"]
      |> Enum.map(&Task.async(fn -> Servy.VideoCam.get_snapshot(&1) end))
      |> Enum.map(&Task.await/1)  

	where_is_bigfoot = Task.await(task)
    %{snapshots: snapshots, location: where_is_bigfoot}
  end
end
```
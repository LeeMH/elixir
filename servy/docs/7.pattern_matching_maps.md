# Pattern Matching Maps

## 강의 주요 내용

* map에 대한 패턴 매칭
* rewrite_path 함수 추가
* route 함수 개선

### 1. map에 대한 패턴 매칭

```elixir
iex(2)> conv = %{path: "/wildthings", status: 200, resp_body: "hello"}
%{status: 200, path: "/wildthings", resp_body: "hello"}

## 좌측에 매칭 시킬 key와 value를 넣고 비교한다.
## 이땨, map의 전체 key, value가 아닌 원하는 key, value만 넣으면 된다.
iex(3)> %{path: "/wildthings"} = conv
%{status: 200, path: "/wildthings", resp_body: "hello"}

## status 처럼 변수형태로 패턴매칭도 가능하다.
iex(4)> %{path: "/wildthings", status: status} = conv
%{status: 200, path: "/wildthings", resp_body: "hello"}

iex(5)> status
200

## 당연히 매칭되지 않으면 에러가 발생한다.
iex(6)> %{path: "/hello", status: status} = conv
** (MatchError) no match of right hand side value: %{status: 200, path: "/wildthings", resp_body: "hello"}
    (stdlib 5.2.1) erl_eval.erl:498: :erl_eval.expr/6
    iex:6: (file)
iex(6)> 
```

### 2. rewrite_path 함수 추가

```elixir

## path가 "/wildlife" 인경우, /wildthings로 rewrite 한다.
def rewrite_path(%{path: "/wildlife"} = conv) do
  %{ conv | path: "/wildthings" }
end  

## default function clause는 필수 이다.
def rewrite_path(conv), do: conv
```

### 3. route 함수 개선

```elixir
## 기존 route 함수는 1개의 conv 파라메터를 받아, 다시 route(conv, conv.method, conv.path) 호출해 주는 형식이있다.
## map pattern matching을 이용해서 직접 라우팅을 하도록 개선
def route(%{method: "GET", path: "/wildthings"} = conv) do
  %{ conv | status: 200, resp_body: "Bears, Lions, Tigers"}
end  

def route(%{method: "GET", path: "/bears"} = conv) do
  %{ conv | status: 200, resp_body: "Teddy, Smokey, Paddington"}
end

def route(%{method: "GET", path: "/bears/" <> id} = conv) do
  %{ conv | status: 200, resp_body: "bear #{id}"}
end

def route(conv) do
  %{ conv | status: 404, resp_body: "no #{conv.path} here"}
end
```
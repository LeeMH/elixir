# Button Clicks

## 강의 주요 내용

* LiveView route
* LiveView mount, render, handle_event 예제
* 상태 저장 방법
* HEEx 변수 지정 방법

## 1. LiveView route

* 프로젝트 폴더 > lib > {프로젝트명}_web > router.ex
![[Pasted image 20240423111346.png]]

```elixir
  scope "/", LiveViewStudioWeb do
    # pipe_through: broswer 부분에 매핑을 추가한다.
    pipe_through :browser  

    get "/", PageController, :home  

    ## 경로, 처리모듈 형식으로 추가한다.
    live "/light", LightLive
    live "/sandbox", SandboxLive
    ...
    ...
  end
```


## 2. LiveView mount, render, handle_event 예제

* 모듈은 mount, render, handle_event 3개의 함수로 이뤄진다.
  1. mount : 초기 상태값을 정의
  2. render : view 영역을 담당. HEEx 템플릿을 사용한다.
  3. handle_event : 브라우저 요청을 처리하며, 패턴매칭으로 각각의 이벤트 처리 함수가 구현된다.

```elixir
defmodule LiveViewStudioWeb.LightLive do
  ## LiveView 사용을 명시
  use LiveViewStudioWeb, :live_view  

  ## 초기 상태값을 정의한다.
  def mount(_params, _session, socket) do
    {:ok, assign(socket, :brightness, 10)}
  end  

  ## HEEx 템플릿으로 view 영역을 처리한다.
  def render(assigns) do
    ~H"""
    <h1>Front Porch Light</h1>
    <div id="light">
      <div class="meter">
        <span style={"width: #{assigns.brightness}%"}>
          <!--
          <%= @brightness %>%
          -->
          <%= assigns.brightness %>%
        </span>
      </div>  

      <button phx-click="off">
        <img src="/images/light-off.svg" />
      </button>

      <button phx-click="down">
        <img src="/images/down.svg" />
      </button>
      
      <button phx-click="up">
        <img src="/images/up.svg" />
      </button>

      <button phx-click="on">
        <img src="/images/light-on.svg" />
      </button>
    </div>
    """
  end

  ## 브라우져 요청을 처리한다.
  ## HEEx의 phx-click 값이 arity 첫번째 값과 매칭된다.
  def handle_event("on", _payload, socket) do
    socket = assign(socket, brightness: 100)
    {:noreply, socket}
  end  

  def handle_event("up", _payload, socket) do
    socket = update(socket, :brightness, &(&1 + 10))
    {:noreply, socket}
  end  

  def handle_event("off", _payload, socket) do
    socket = assign(socket, brightness: 0)
    {:noreply, socket}
  end

  def handle_event("down", _payload, socket) do
    socket = update(socket, :brightness, &(&1 - 10))
    {:noreply, socket}
  end
end
```


## 3. 상태 저장 방법

* assign/2, assign/3 를 모두 사용할 수 있다.
```elixir
assign(socket, brightness: 10)

assign(socket, :brightness, 10)
```

* 반면 업데이트에서는 update/3 만 사용할 수 있다.
```elixir
socket = update(socket, :brightness, &(&1 - 10))
```

* 아직 phoenix framework에 익숙하지 않을 때, 에러가 발생하면 상당히 혼란스럽다.

## 4. HEEx 변수 지정 방법

* HEEx 템플릿에서 변수 지정 방법도 혼동하기 쉽다.
* 쉽게 정리하면, HTML Tag 밖에서는 `<% 변수 %>` 형태로 사용한다.
* 반면 Tag 안에서 사용시에는 `{"width: #{@brightness}"}%` 의 형태로 사용한다.
* 추가로 `@`은 assign을 의미한다.
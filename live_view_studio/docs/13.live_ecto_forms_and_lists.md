# Live Ecto Forms and Lists

## 강의 주요 내용

* Ecto를 이용한 입력 form 추가
* data validation
* data insert 처리
* 화면 갱신
* input form 생성 추가

![[Pasted image 20240429122121.png]]

## 1. Ecto를 이용한 입력 form 추가

* 우선 iex를 통해 Ecto 검증 메커니즘을 확인해 보자.
* Ecto context(repo) 에서는 변경 전 `Volunteer.changeset` 함수를 호출하여 데이터를 검증한다.
```elixir
## 빈 Volunteer를 생성
iex(3)> v = %Volunteer{}
%LiveViewStudio.Volunteers.Volunteer{
  __meta__: #Ecto.Schema.Metadata<:built, "volunteers">,
  id: nil,
  name: nil,
  phone: nil,
  checked_out: false,
  inserted_at: nil,
  updated_at: nil
}

## change_volunteer 함수를 호출
## 결과에 changes 항목에는 실제 변경된 항목
## errors에는 검증에 실패한 목록이 리턴된다.
iex(4)> changeset = Volunteers.change_volunteer(v)
#Ecto.Changeset<
  action: nil,
  changes: %{},
  errors: [
    name: {"can't be blank", [validation: :required]},
    phone: {"can't be blank", [validation: :required]}
  ],
  data: #LiveViewStudio.Volunteers.Volunteer<>,
  valid?: false
>
```


* 아래와 같이 동일하게 필수 입력 항목이 실패한 내용을 form으로 바인딩한다.
```elixir
  def mount(_params, _session, socket) do
    volunteers = Volunteers.list_volunteers()
    ## valid 실패한 내역을 to_form 함수를 이용해서 :form으로 전달
    changeset = Volunteers.change_volunteer(%Volunteer{})  

    socket =
      assign(socket,
        volunteers: volunteers,
        form: to_form(changeset)
      ) 

    {:ok, socket}
  end
```

* 아래와 같이 form을 생성한다.
```html
    <h1>Volunteer Check-In</h1>
    <div id="volunteer-checkin">
      ## 전달된 form(@form)을 이용해서 form을 생성한다. for 스펠링에 주의!!
      <.form for={@form} phx-submit="save">
        ## 각각의 form을 추가
        <.input field={@form[:name]} placeholder="Name" autocomplete="off" >
        <.input field={@form[:phone]} type="tel" placeholder="Phone" autocomplete="off" />
        ## phx-disable-with 속성을 사용해서 loading 효과를 줄수 있는것 같다.
        <.button phx-disable-with="Saving...">
          Check In
        </.button>
      </.form>
```

## 2. data validation

* 아래는 Volunteer.changeset 함수이다.
* 엔티티에 대한 data validation을 수행하는 함수이다.
```elixir
  @phone ~r/^\d{3}[\s-.]?\d{3}[\s-.]?\d{4}$/  

  @doc false
  def changeset(volunteer, attrs) do
    volunteer
    |> cast(attrs, [:name, :phone, :checked_out])
    |> validate_required([:name, :phone])
    |> validate_length(:name, min: 2, max: 100, message: "이름을 2~100자 사이로 입력해주세요.")
    |> validate_format(:phone, @phone, message: "must be a valid phone number")
  end
```

* 아래는 생성된 Ecto Context(repo)의 update 함수이다.
* 당연히 changeset 함수를 호출한다.
```elixir
  def update_volunteer(%Volunteer{} = volunteer, attrs) do
    volunteer
    |> Volunteer.changeset(attrs)
    |> Repo.update()
  end
```

## 3. data insert 처리

* `phx-submit="save"`를 통해 save 이벤트로 서버에 전송된다.
* creat_volunteer 함수를 수행하고, `:ok`와 `:error`에 따라 분기 한다.

```elixir
  def handle_event("save", %{"volunteer" => volunteer_params}, socket) do
    ## insert 수행
    case Volunteers.create_volunteer(volunteer_params) do
      ## 정상 이라면
      {:ok, volunteer} ->
        socket =
          update(socket,
            :volunteers,
            &([volunteer | &1])
          )
        changeset = Volunteers.change_volunteer(%Volunteer{})
        {:noreply, assign(socket, :form, to_form(changeset))}
      ## 에러 발생시
      {:error, changeset} ->
        ## to_form 함수를 이용해 화면에 표시
        {:noreply, assign(socket, :form, to_form(changeset))}
    end
  end
```


## 4. 화면 갱신

* 위 로직에서 :ok 로직만 자세히 보자
* 우선 방금 입력된 데이터를 화면 최상단에 넣어준다. `hd | tl` 기능을 이용하여 update
* 입력창 초기화를 위해, mount 함수에서와 동일하게 form 필드를 초기화 한다.
```elixir
      {:ok, volunteer} ->
        socket =
          update(socket,
            :volunteers,
            &([volunteer | &1])
          )
        changeset = Volunteers.change_volunteer(%Volunteer{})
        {:noreply, assign(socket, :form, to_form(changeset))}
```


## 5. input form 생성 추가

* Ecto 사용하지 않고, form 만들기
```elixir
def mount(_params, _session, socket) do
  form = to_form(%{"email" => ""})
  {:ok, assign(socket, :form, form)}
end

def render(assigns) do
  ~H"""
  <div>
    <.form for={@form} phx-submit="save">
      <.input field={@form[:email]} />
      <.button>Save</.button>
    </.form>
  </div>
  """
end
```

* nested param, user.email 형태로 전달가능
```elixir
def mount(_params, _session, socket) do
  form = to_form(%{"email" => ""}, as: "user")  {:ok, assign(socket, :form, form)}
end

def handle_event("save", %{"user" => %{"email" => email}}, socket) do
  # ...
  {:noreply, socket}
end
```
# Pagination

## 강의 주요 내용

* pagination page 구현
* sort_link 함수 개선
* pagination per_page 구현
* push_patch

![[Pasted image 20240426141351.png]]

## 1. pagination page 구현

### 1.1 repo 구조

* 아래와 같이 sort_by, sort_order와 `page`, `per_page` 항목이 list_donation의 parameter로 지정된다.
* pagination 함수의 구현을 보면 첫 페이지는 1로 셋팅된다.

```elixir
  @doc """
  Returns a list of donations based on the given `options`.  

  Example options:  

  %{sort_by: :item, sort_order: :asc, page: 2, per_page: 5}
  """
  def list_donations(options) when is_map(options) do
    from(Donation)
    |> sort(options)
    |> paginate(options)
    |> Repo.all()
  end
  
  defp paginate(query, %{page: page, per_page: per_page}) do
    offset = max((page - 1) * per_page, 0)
    query
    |> limit(^per_page)
    |> offset(^offset)
  end
```

### 1.2 HEEx 템플릿 구조

* Next 클릭시 page+1, Previous 클릭시 page-1 을 넣고 patch 한다.
* 이미 options에 정렬,페이징 정보가 있기 때문에 `갱신 연산자(|)`를 통해 page 항목만 갱신한다.
```html
      <div class="pagination">
        <.link
          ## 1페이지 보다 큰경우만 출력되도록 설정
          :if={@options.page > 1}
          patch={          
          ~p"/donations?#{%{@options | page: @options.page-1}}"
        }>
          Previous
        </.link>  

        <.link patch={
          ~p"/donations?#{%{@options | page: @options.page+1}}"
        }>
          Next
        </.link>
      </div>
```

### 1.3 live view 모듈 내용

* page와 per_page 항목을 추가한다.
```elixir
  def handle_params(params, _uri, socket) do
    sort_by = (params["sort_by"] || "id") |> String.to_atom()
    sort_order = (params["sort_order"] || "asc") |> String.to_atom()  

    ## page, per_page 컬럼을 상태에 추가한다.
    page = (params["page"] || "1") |> String.to_integer()
    per_page = (params["per_page"] || "5") |> String.to_integer()
 
	options = %{
      sort_by: sort_by,
      sort_order: sort_order,
      page: page,
      per_page: per_page
    }
    ...
    ...
  end
```


## 2. sort_link 함수 개선

* 총 4개의 필드를 ~p 시길을 이용해서 구현하면 너무 복잡하기 때문에, params map으로 개선한다.
* 지역변수인 params를 생성해서, 다시 assigns.params에 매핑한다. 직접 넣어주면 될것 같은데 왜 이리 복잡하게 하는 것 일까??
* `~H 시길`에서는 `지역변수에 접근할 수 없다`.
* 따라서, `socket.assigns`에 값을 할당하고, `@params`로 접근해야 한다.
```elixir
  def sort_link(assigns) do
    params = %{
      assigns.options
      | sort_by: assigns.sort_by, sort_order: next_sort_order(assigns.options.sort_order)
    }  

    ## params를 다시 assigns.params 로 매핑한다.
    assigns = assign(
      assigns,
        params: params
      )  

    ~H"""
    <.link patch={
      ~p"/donations?#{@params}"
    }>
      <%= render_slot(@inner_block) %>
    </.link>
    """
  end
```

## 3. pagination per_page 구현

### 3.1 HEEx 템플릿 구조

* `Phoenix.HTML.Form.options_for_select` 함수를 이용해서, 페이지 사이즈를 만들고, options.per_page 항목에 연동한다.
* 값이 변경되는 경우 `select-per-page` 이벤트를 발생 시킨다.
```html
  <form name="per-page" phx-change="select-per-page">
    <select name="per-page">
      <%= Phoenix.HTML.Form.options_for_select(
        [5, 10, 15, 20],
        @options.per_page
      ) %>
    </select>
    <lable for="per-page">per page</lable>
  </form>
```

1.2 live view 모듈 내용

* per_page 값이 변경되었지만, 리스트의 내용도 변경되어야 한다.
* next 버튼이나 sort 버튼 클릭시, .link의 patch로 전송되어 리스트가 전송된다.
* 하지만, per_page는 `handle_event`로 처리해 주어야 한다.
* url도 변경되어야 하고, 리스트도 변경되어야 한다. 이것을 한번에 구현해 주는 것이 `push_patch`이다.
```elixir
def handle_event("select-per-page", %{"per-page" => per_page}, socket) do
    params = %{socket.assigns.options | per_page: per_page}
    socket = push_patch(socket, to: ~p"/donations?#{params}") 

    {:noreply, socket}
end
```


## 4. push_patch

* `push_patch` 는 변경된 상태만 내리고, 해당 값으로 patch를 하도록 유도한다.
* 아래 캡쳐와 같이, `1번의 아웃바운드`에 `2개의 인바운드`가 전송된다.
* 첫번째 인바운드는 URL 변경에 대한 정보이고, 두번째 인바운드는 데이터 변경에 대한 메세지 이다.
![[Pasted image 20240426143800.png]]
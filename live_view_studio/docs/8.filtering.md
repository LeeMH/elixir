# Filtering

## 강의 주요 내용

* Ecto 모듈 연동
* Filtering 사용법
* temporary_assign 옵션 사용법
* Ecto DB migration cli

![[Pasted image 20240424122500.png]]

### 1. Ecto 모듈 연동

* ecto는 phoenix에서 사용하는 orm 같은 모듈이다.
* ecto는 phoenix 또는 elixir를 사용하려면, 반드시 익혀야 할 모듈중 하나이다.
* [공식문서](https://hexdocs.pm/ecto/Ecto.html)를 참조하자!!

### 2. Filtering 사용법

```elixir

  ## filter 이벤트를 처리한다.
  def handle_event("filter", %{"type" => type, "prices" => prices}, socket) do
    filter = %{type: type, prices: prices}
    ## Boat Ecto 모듈을 이용해서 data를 fetch 한다.
    boats = Boats.list_boats(filter)
    {:noreply, assign(socket, boats: boats, filter: filter)}
  end
```

* 실제 구현된 list_boats 함수이다.
```elixir

  @doc """
  Returns a list of boats matching the given `filter`. 
  Example filter:
  %{type: "sporting", prices: ["$", "$$"]}
  """
  def list_boats(filter) when is_map(filter) do
    from(Boat)
    |> filter_by_type(filter)
    |> filter_by_prices(filter)
    |> Repo.all()
  end

  ## 필터링 값이 없는경우, input query를 그대로 리턴한다.
  defp filter_by_type(query, %{type: ""}), do: query  
  defp filter_by_type(query, %{type: type}) do
    where(query, type: ^type)
  end  

  ## 마찮가지로, prices 옵션이 없다면 query를 그대로 리턴한다.
  defp filter_by_prices(query, %{prices: [""]}), do: query
  defp filter_by_prices(query, %{prices: prices}) do
    where(query, [boat], boat.price in ^prices)
  end
```

* 기본적으로 query등 모든 디버깅에 필요한 내용이 출력 된다.
![[Pasted image 20240424123411.png]]

### 3. temporary_assign 옵션 사용법

* phoenix 에서는 모든 상태가 서버에서 관리된다.
* 출력 된 리스트조차 socket에 바인딩되어 관리된다.
* 상태 필터는 상태가 유지되어야 하지만, 리스트는 렌더링 후 전혀 사용되지 않는다.
* 문제는 수많은 사용자가 있는 경우, `메모리에 심각한 문제`가 발생할 수 있다.
* 이를 해결하기 위해, `temporary_assign` 옵션을 사용한다.

```elixir
  def mount(_params, _session, socket) do
    socket =
      assign(socket,
        filter: %{type: "", prices: []},
        boats: Boats.list_boats()
      )  

    ## boats는 렌더링이 종료되면 empty list로 처리한다
    {:ok, socket, temporary_assign: [boats: []]}
  end
```


### 4. Ecto DB migration

```bash
$ mix phx.gen.context Cars Car cars model:string type:string price:string image:string
* creating lib/live_view_studio/cars/car.ex
* creating priv/repo/migrations/20240424050655_create_cars.exs
* creating lib/live_view_studio/cars.ex
* injecting lib/live_view_studio/cars.ex
* creating test/live_view_studio/cars_test.exs
* injecting test/live_view_studio/cars_test.exs
* creating test/support/fixtures/cars_fixtures.ex
* injecting test/support/fixtures/cars_fixtures.ex
```

* Schema 파일 (JPA의 entity와 비슷한 역할을 하는 것 같음)
```elixir
defmodule LiveViewStudio.Cars.Car do
  use Ecto.Schema
  import Ecto.Changeset  

  schema "cars" do
    field :type, :string
    field :image, :string
    field :model, :string
    field :price, :string  

    timestamps(type: :utc_datetime)
  end  

  @doc false
  def changeset(car, attrs) do
    car
    |> cast(attrs, [:model, :type, :price, :image])
    |> validate_required([:model, :type, :price, :image])
  end
end
```

* Repo 파일 (JPA의 repository와 비슷한 역할을 하는 것 같음)
```elixir
defmodule LiveViewStudio.Cars do
  @moduledoc """
  The Cars context.
  """
  
  import Ecto.Query, warn: false
  alias LiveViewStudio.Repo
  alias LiveViewStudio.Cars.Car  

  @doc """
  Returns the list of cars.  

  ## Examples
      iex> list_cars()
      [%Car{}, ...]
  """
  def list_cars do
    Repo.all(Car)
  end  

  @doc """
  Gets a single car.
  Raises `Ecto.NoResultsError` if the Car does not exist. 

  ## Examples
      iex> get_car!(123)
      %Car{}  

      iex> get_car!(456)
      ** (Ecto.NoResultsError)
  """
  def get_car!(id), do: Repo.get!(Car, id)  

  @doc """
  Creates a car. 

  ## Examples
      iex> create_car(%{field: value})
      {:ok, %Car{}} 

      iex> create_car(%{field: bad_value})
      {:error, %Ecto.Changeset{}}
  """
  def create_car(attrs \\ %{}) do
    %Car{}
    |> Car.changeset(attrs)
    |> Repo.insert()
  end  

  @doc """
  Updates a car.  

  ## Examples
      iex> update_car(car, %{field: new_value})
      {:ok, %Car{}}  

      iex> update_car(car, %{field: bad_value})
      {:error, %Ecto.Changeset{}}
  """
  def update_car(%Car{} = car, attrs) do
    car
    |> Car.changeset(attrs)
    |> Repo.update()
  end  

  @doc """
  Deletes a car.  

  ## Examples
      iex> delete_car(car)
      {:ok, %Car{}}  

      iex> delete_car(car)
      {:error, %Ecto.Changeset{}}
  """
  def delete_car(%Car{} = car) do
    Repo.delete(car)
  end  

  @doc """
  Returns an `%Ecto.Changeset{}` for tracking car changes.  

  ## Examples
      iex> change_car(car)
      %Ecto.Changeset{data: %Car{}}
  """
  def change_car(%Car{} = car, attrs \\ %{}) do
    Car.changeset(car, attrs)
  end
end
```

* Entity 변경사항은 마이그레이션 파일로 추적되고 아래와 같이 기록된다.

```bash
## DB schema를 초기화 한다. 
## 이때, 아래의 마이그레이션 파일을 모두 실행한다.
## seeds.exs는 초기화 파일이다.(초기 데이터 load)
$ mix ecto.reset
```

![[Pasted image 20240424144522.png]]